# Maintainer: pyther <pyther@pyther.net>
# Contributor: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>
# Contributor: xintron <carlsson.marcus@gmail.com>

pkgname=spotify-beta
pkgver=0.8.2.572
_anotherpkgver=.geb65f9a.433-1
pkgrel=1
pkgdesc="A proprietary peer-to-peer music streaming service"
makedepends=()
arch=('x86_64' 'i686')
license=('custom:Copyright (c) 2006-2010 Spotify Ltd')
url="http://www.spotify.com"
changelog='spotify.changelog'
conflicts=('spotify')
options=(!strip)
install='spotify.install'

if [ "${CARCH}" = "x86_64" ]; then
  makedepends=(rpmextract)
  md5sums=('fc5b4f3c80be2c229169fef4b559c71f'
         '617f76866c4c0a3dc1ca3996d93676ac'
         '37ef199620c3257d1a3e3fb304f4d9b9'
         '4d1506851cdff3a8f82da32b08df131c'
         '93a3e319f42b0594b80a140b6037363a'
         'dedc9038d009da84ba889ddb3794a74c'
         '331b4009339a2f14bd0f7c5aa6a6e653'
         '3ac14444d45d9caeef6f0515405ee8aa')
  _carch=_amd64
  libarch=x86_64-linux-gnu
  flashurl="http://apt.sw.be/redhat/el5/en/x86_64/rpmforge/RPMS/flash-plugin-10.3.162.29-0.1.el5.rf.x86_64.rpm"
elif [ "${CARCH}" = "i686" ]; then
  md5sums=('9dbcd82e8a6aa2b0c773b64a2882217f'
         'e0858bcbf64c6153c937197002d1c62d'
         'e2cc39229a453fb3da9662dcb87a2ad4'
         '6e9adba486db2f958fb347ce9d772f09'
         '6cf1dde9dee8f4f7b034a4e13400e857'
         '583e531ba01663791a2eaa29c3caa950'
         '63b04fe46376fa657f5d59e987bcf23b'
         '3ac14444d45d9caeef6f0515405ee8aa')
  _carch=_i386
  libarch=i386-linux-gnu
  flashurl="http://mirror.sim.ul.pt/debian-multimedia/pool/non-free/f/flash-player/flash-player_10.3.183.10.orig.tar.gz"
fi

depends=("alsa-lib>=1.0.14" "qt>=4.5.0" "libxss" "openssl" "qtwebkit" "gtk2" "gconf" "libgcrypt")
source=("http://download.spotify.com/preview/spotify-client_${pkgver}${_anotherpkgver}${_carch}.deb"
        "http://mirrors.us.kernel.org/ubuntu/pool/main/libp/libpng/libpng12-0_1.2.46-3ubuntu1${_carch}.deb"
        "http://mirrors.us.kernel.org/ubuntu/pool/main/n/nspr/libnspr4_4.8.7-0ubuntu3${_carch}.deb"
        "http://mirrors.us.kernel.org/ubuntu/pool/main/n/nss/libnss3_3.12.9+ckbi-1.82-0ubuntu6${_carch}.deb"
        "http://mirrors.us.kernel.org/ubuntu/pool/universe/o/openssl098/libssl0.9.8_0.9.8o-7ubuntu1${_carch}.deb"
        "http://mirrors.us.kernel.org/ubuntu/pool/main/q/qtwebkit-source/libqtwebkit4_2.2~2011week36-0ubuntu1${_carch}.deb"
        $flashurl
        'spotify' )

build() {
  cd ${srcdir}

  # Extract Spotify Sources
  install -d "${srcdir}/spotifydeb"
  ar x spotify-client_${pkgver}${_anotherpkgver}${_carch}.deb
  bsdtar xf data.tar.gz -C "${srcdir}/spotifydeb"

  # libpng12 - ubuntu oneiric
  install -d "${srcdir}/libpng12"
  ar x "${srcdir}/libpng12-0_1.2.46-3ubuntu1${_carch}.deb"
  bsdtar xf data.tar.gz -C "${srcdir}/libpng12"

  # libnspr4 - ubuntu oneiric
  install -d "${srcdir}/libnspr4"
  ar x "${srcdir}/libnspr4_4.8.7-0ubuntu3${_carch}.deb"
  bsdtar xf data.tar.gz -C "${srcdir}/libnspr4"

  # libnss3 - ubuntu oneiric
  install -d "${srcdir}/libnss3"
  ar x "${srcdir}/libnss3_3.12.9+ckbi-1.82-0ubuntu6${_carch}.deb"
  bsdtar xf data.tar.gz -C "${srcdir}/libnss3"

  # openssl 0.98 - ubuntu oneiric
  install -d "${srcdir}/openssl"
  ar x "${srcdir}/libssl0.9.8_0.9.8o-7ubuntu1${_carch}.deb"
  bsdtar xf data.tar.gz -C "${srcdir}/openssl"

  # qtwebkit - ubuntu oneiric
  install -d "${srcdir}/libqtwebkit"
  ar x "${srcdir}/libqtwebkit4_2.2~2011week36-0ubuntu1${_carch}.deb"
  bsdtar xf data.tar.lzma -C "${srcdir}/libqtwebkit"

  # Flash Plugin 10.3.183.5
  install -d "${srcdir}/flash"
  if [ "${CARCH}" = "x86_64" ]; then
    rpm2cpio flash-plugin-10.3.162.29-0.1.el5.rf.x86_64.rpm | bsdtar --include '*libflashplayer.so' --strip-components 4 -C "${srcdir}/flash" -xf -
  elif [ "${CARCH}" = "i686" ]; then
    tar xf "${srcdir}/flash-player_10.3.183.10.orig.tar.gz" -C "${srcdir}/flash" --strip-components=1
  fi
}

package() {
  cd ${srcdir}

  # Install Spotify Files
  install -d ${pkgdir}/usr/share/
  cp -r "${srcdir}/spotifydeb/usr/share/spotify/" "${pkgdir}/usr/share"
  cp -r "${srcdir}/spotifydeb/usr/share/pixmaps/" "${pkgdir}/usr/share"
  cp -r "${srcdir}/spotifydeb/usr/share/applications/" "${pkgdir}/usr/share"

  # Bin Script
  install -d "${pkgdir}/usr/bin"
  install -D -m755 "${srcdir}/spotify" "${pkgdir}/usr/bin"

  # Install Ubuntu Libs
  install -d "${pkgdir}/usr/share/spotify/libs"

  # libnspr4.so
  install -D -m644 "${srcdir}/libnspr4/usr/lib/$libarch/libnspr4.so" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libnspr4.so "${pkgdir}/usr/share/spotify/libs/libnspr4.so.0d"

  # libplc4.so
  install -D -m644 "${srcdir}/libnspr4/usr/lib/$libarch/libplc4.so" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libplc4.so "${pkgdir}/usr/share/spotify/libs/libplc4.so.0d"

  # libnss3.so
  install -D -m644 "${srcdir}/libnss3/usr/lib/$libarch/libnss3.so" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libnss3.so "${pkgdir}/usr/share/spotify/libs/libnss3.so.1d"

  # libnssutil3.so
  install -D -m644 "${srcdir}/libnss3/usr/lib/$libarch/libnssutil3.so" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libnssutil3.so "${pkgdir}/usr/share/spotify/libs/libnssutil3.so.1d"

  # libsmime3.so
  install -D -m644 "${srcdir}/libnss3/usr/lib/$libarch/libsmime3.so" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libsmime3.so "${pkgdir}/usr/share/spotify/libs/libsmime3.so.1d"

  # libpng12.so
  install -D -m644 "${srcdir}/libpng12/lib/$libarch/libpng12.so.0.46.0" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libpng12.so.0.46.0 "${pkgdir}/usr/share/spotify/libs/libpng12.so.0"

  # libqtwebkit3.so
  install -D -m644 "${srcdir}/libqtwebkit/usr/lib/$libarch/libQtWebKit.so.4.9.0" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libQtWebKit.so.4.9.0 "${pkgdir}/usr/share/spotify/libs/libQtWebKit.so.4"

  # libcrypto.so
  install -D -m644 "${srcdir}/openssl/lib/libcrypto.so.0.9.8" \
    "${pkgdir}/usr/share/spotify/libs/"

  # libssl.so
  install -D -m644 "${srcdir}/openssl/lib/libssl.so.0.9.8" \
    "${pkgdir}/usr/share/spotify/libs/"

  # flash
  install -d "${pkgdir}/usr/share/spotify/mozplugins"
  install -D -m644 "${srcdir}/flash/libflashplayer.so" \
    "${pkgdir}/usr/share/spotify/mozplugins/"

  # Copy license
  install -d ${pkgdir}/usr/share/licenses/spotify-beta
  install -D -m644 ${srcdir}/spotifydeb/usr/share/doc/spotify-client/copyright \
    ${pkgdir}/usr/share/licenses/spotify-beta/
}

