# Maintainer: pyther <pyther@pyther.net>
# Contributor: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>
# Contributor: xintron <carlsson.marcus@gmail.com>

pkgname=spotify-beta
pkgver=0.8.2.637
_anotherpkgver=.g252b980.486-1
pkgrel=2
pkgdesc="A proprietary peer-to-peer music streaming service"
makedepends=()
arch=('x86_64' 'i686')
license=('custom:Copyright (c) 2006-2010 Spotify Ltd')
url="http://www.spotify.com"
conflicts=('spotify')
options=(!strip)

if [ "${CARCH}" = "x86_64" ]; then
  md5sums=('96f4f950555cad73fcbf29c81d5e2957'
         '617f76866c4c0a3dc1ca3996d93676ac'
         'dedc9038d009da84ba889ddb3794a74c'
         '283d389f418b8a36629d9bb2b6476a17')
  _carch=_amd64
  libarch=x86_64-linux-gnu
elif [ "${CARCH}" = "i686" ]; then
  md5sums=('0fabe525f867da4fac0cb7da3132291f'
         'e0858bcbf64c6153c937197002d1c62d'
         '583e531ba01663791a2eaa29c3caa950'
         '283d389f418b8a36629d9bb2b6476a17')
  _carch=_i386
  libarch=i386-linux-gnu
fi

depends=("alsa-lib>=1.0.14" "qt>=4.5.0" "libxss" "openssl098" "qtwebkit" "gtk2" "gconf" "libgcrypt" "nss" "nspr")
source=("http://repository.spotify.com/pool/non-free/s/spotify/spotify-client_${pkgver}${_anotherpkgver}${_carch}.deb"
        "http://mirrors.us.kernel.org/ubuntu/pool/main/libp/libpng/libpng12-0_1.2.46-3ubuntu1${_carch}.deb"
        "http://mirrors.us.kernel.org/ubuntu/pool/main/q/qtwebkit-source/libqtwebkit4_2.2~2011week36-0ubuntu1${_carch}.deb"
        'spotify' )

build() {
  cd ${srcdir}

  # Extract Spotify Sources
  install -d "${srcdir}/spotifydeb"
  ar x spotify-client_${pkgver}${_anotherpkgver}${_carch}.deb
  bsdtar xf data.tar.gz -C "${srcdir}/spotifydeb"

  # libpng12 - ubuntu oneiric
  install -d "${srcdir}/libpng12"
  ar x "${srcdir}/libpng12-0_1.2.46-3ubuntu1${_carch}.deb"
  bsdtar xf data.tar.gz -C "${srcdir}/libpng12"

  # qtwebkit - ubuntu oneiric
  install -d "${srcdir}/libqtwebkit"
  ar x "${srcdir}/libqtwebkit4_2.2~2011week36-0ubuntu1${_carch}.deb"
  bsdtar xf data.tar.lzma -C "${srcdir}/libqtwebkit"
}

package() {
  cd ${srcdir}

  # Install Spotify Files
  install -d ${pkgdir}/usr/share/
  cp -r "${srcdir}/spotifydeb/usr/share/spotify/" "${pkgdir}/usr/share"
  cp -r "${srcdir}/spotifydeb/usr/share/pixmaps/" "${pkgdir}/usr/share"
  cp -r "${srcdir}/spotifydeb/usr/share/applications/" "${pkgdir}/usr/share"

  # Bin Script
  install -d "${pkgdir}/usr/bin"
  install -D -m755 "${srcdir}/spotify" "${pkgdir}/usr/bin"

  # Install Ubuntu Libs
  install -d "${pkgdir}/usr/share/spotify/libs"

  # libpng12.so
  install -D -m644 "${srcdir}/libpng12/lib/$libarch/libpng12.so.0.46.0" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libpng12.so.0.46.0 "${pkgdir}/usr/share/spotify/libs/libpng12.so.0"

  # libqtwebkit3.so
  install -D -m644 "${srcdir}/libqtwebkit/usr/lib/$libarch/libQtWebKit.so.4.9.0" \
    "${pkgdir}/usr/share/spotify/libs/"
  ln -s ./libQtWebKit.so.4.9.0 "${pkgdir}/usr/share/spotify/libs/libQtWebKit.so.4"

  ln -s /usr/lib/libnspr4.so "${pkgdir}/usr/share/spotify/libs/libnspr4.so.0d"
  ln -s /usr/lib/libplc4.so "${pkgdir}/usr/share/spotify/libs/libplc4.so.0d"
  ln -s /usr/lib/libnss3.so "${pkgdir}/usr/share/spotify/libs/libnss3.so.1d"
  ln -s /usr/lib/libnssutil3.so "${pkgdir}/usr/share/spotify/libs/libnssutil3.so.1d"
  ln -s /usr/lib/libsmime3.so "${pkgdir}/usr/share/spotify/libs/libsmime3.so.1d"

  # Copy license
  install -d ${pkgdir}/usr/share/licenses/spotify-beta
  install -D -m644 ${srcdir}/spotifydeb/usr/share/doc/spotify-client/copyright \
    ${pkgdir}/usr/share/licenses/spotify-beta/
}

